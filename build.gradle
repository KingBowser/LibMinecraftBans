apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'signing'

group   = 'pw.bowser'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'

    testCompile "junit:junit:4.11"
}

task docJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives docJar
    archives sourceJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment dep -> signing.signPom(dep) }

            /*
             * Store your username and password for sonatype OSS in ~/.gradle/gradle.properties like this:
             * sonatype.username               = username
             * sonatype.password               = password
             */
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.'sonatype.username', password: project.properties.'sonatype.password')
            }

            pom.project {
                name 'libMinecraftBans'
                packaging 'jar'
                description 'API for querying about minecraft bans.'
                url 'http://bowser.pw/projects/minecraftBans'

                scm {
                    url 'https://bitbucket.org/kingbowser/libminecraftbans'
                    connection 'scm:git@bitbucket.org:kingbowser/libminecraftbans.git'
                    developerConnection 'scm:git@bitbucket.org:kingbowser/libminecraftbans.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'bowser'
                        name 'Bowser'
                    }
                }
            }
        }
    }
}